plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven {
        url = 'https://maven.shedaniel.me/'
        content {
            includeGroup 'me.shedaniel.cloth'
        }
    }

    maven {
        url = 'https://maven.terraformersmc.com/releases/'
        content {
            includeGroup 'com.terraformersmc'
        }
    }

    maven {
        url = 'https://repo.hypixel.net/repository/Hypixel/'
        content {
            includeGroup 'net.hypixel'
        }
    }
}

loom {
    mods {
        "darkutils" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi('me.shedaniel.cloth:cloth-config-fabric:20.0.149') {
        exclude(group: 'net.fabricmc.fabric-api')
    }

    modApi('com.terraformersmc:modmenu:15.0.0') {
        exclude(group: 'net.fabricmc.fabric-api')
    }

    modApi('net.hypixel:mod-api:1.0.1') {
        exclude(group: 'net.fabricmc.fabric-api')
    }
}

processResources {
    // Capture version at configuration time
    final def modVersion = project.version

    // Declare version for incremental builds
    inputs.property('version', modVersion)

    filesMatching('fabric.mod.json') {
        expand(["version": modVersion])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.named("jar", Jar) {
    manifest {
        attributes(
            "Multi-Release": "true",
            "Sealed": "true",
            "Main-Class": "gg.darkutils.DarkUtils",
            "Automatic-Module-Name": "gg.darkutils",
            "Specification-Title": "DarkUtils",
            "Specification-Version": project.version.toString(),
            "Specification-Vendor": "DarkUtils",
            "Implementation-Title": "DarkUtils",
            "Implementation-Version": project.version.toString(),
            "Implementation-Vendor": "DarkUtils",
            "Name": "gg/darkutils/"
        )
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property 'archivesName', project.base.archivesName
    def archiveName = inputs.properties.archivesName

    from('LICENSE') {
        rename { "${it}_${archiveName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
